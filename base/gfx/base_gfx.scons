# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building base_gfx.lib / libbase_gfx.a.
"""

Import('env')

env = env.Clone()

env.ApplySConscript([
    '$ICU38_DIR/using_icu38.scons',
    '$LIBPNG_DIR/using_libpng.scons',
    '$SKIA_DIR/using_skia.scons',
    '$ZLIB_DIR/using_zlib.scons',
])

if env['PLATFORM'] == 'win32':
  env.Prepend(
      CCFLAGS = [
          '/WX',
      ],
  )

input_files = [
    'gdi_util.cc',
    'native_theme.cc',
    'png_decoder.cc',
    'png_encoder.cc',
    'point.cc',
    'rect.cc',
    'size.cc',
]

if env['PLATFORM'] in ('posix', 'darwin'):
  # Remove files that still need to be ported from the input_files list.
  # TODO(port): delete files from this list as they get ported.
  to_be_ported_files = [
      'gdi_util.cc',
      'native_theme.cc',
  ]
  for remove in to_be_ported_files:
    input_files.remove(remove)

if env['PLATFORM'] == 'win32':
  input_files.extend([
  ])
elif env['PLATFORM'] == 'posix':
  input_files.extend([
  ])

env.ChromeStaticLibrary('base_gfx', input_files)
