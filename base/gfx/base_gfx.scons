# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building base_gfx.lib / libbase_gfx.a.
"""

Import('env')

env = env.Clone()

env.ApplySConscript([
    '$ICU38_DIR/using_icu38.scons',
    '$LIBJPEG_DIR/using_libjpeg.scons',
    '$LIBPNG_DIR/using_libpng.scons',
    '$SKIA_DIR/using_skia.scons',
    '$ZLIB_DIR/using_zlib.scons',
])

if env.Bit('windows'):
  env.Prepend(
      CCFLAGS = [
          '/WX',
      ],
  )

input_files = ChromeFileList([
    'jpeg_codec.cc',
    'jpeg_codec.h',
    'png_decoder.cc',
    'png_decoder.h',
    'png_encoder.cc',
    'png_encoder.h',
    'point.cc',
    'point.h',
    'rect.cc',
    'rect.h',
    'size.cc',
    'size.h',
])

if env.Bit('windows'):
  input_files.Extend([
      'native_theme.cc',
      'native_theme.h',
      'gdi_util.cc',
      'gdi_util.h',
  ])
elif env.Bit('linux'):
  input_files.Extend([
      'gtk_util.cc',
      'gtk_util.h',
  ])

env.ChromeLibrary('base_gfx', input_files)

p = env.ChromeMSVSProject('../build/base_gfx.vcproj',
                          dest='$CHROME_SRC_DIR/base/build/base_gfx.vcproj',
                          guid='{A508ADD3-CECE-4E0F-8448-2F5E454DF551}',
                          keyword='Win32Proj',
                          files=input_files,
                          tools=[
                              'VCPreBuildEventTool',
                              'VCCustomBuildTool',
                              'VCXMLDataGeneratorTool',
                              'VCWebServiceProxyGeneratorTool',
                              'VCMIDLTool',
                              'VCCLCompilerTool',
                              'VCManagedResourceCompilerTool',
                              'VCResourceCompilerTool',
                              'VCPreLinkEventTool',
                              'VCLibrarianTool',
                              'VCALinkTool',
                              'VCXDCMakeTool',
                              'VCBscMakeTool',
                              'VCFxCopTool',
                              'VCPostBuildEventTool',
                          ])

p.AddConfig('Debug|Win32',
            ConfigurationType = '4',
            InheritedPropertySheets = [
                '$(SolutionDir)../build/debug.vsprops',
                './base_gfx.vsprops',
                '../../skia/using_skia.vsprops',
            ])

p.AddConfig('Release|Win32',
            ConfigurationType = '4',
            InheritedPropertySheets = [
                '$(SolutionDir)../build/release.vsprops',
                './base_gfx.vsprops',
                '../../skia/using_skia.vsprops',
            ])
